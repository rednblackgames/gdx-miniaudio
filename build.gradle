buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
        gradlePluginPortal()
        mavenLocal()
        google()
        maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
    }
    dependencies {
        classpath "com.badlogicgames.jnigen:jnigen-gradle:$jniGenVersion"
    }
}

plugins {
    id 'java'
}

apply from: "jni-helpers.gradle"
apply plugin: "com.badlogicgames.jnigen.jnigen-gradle"

group 'games.rednblack.miniaudio'
version '0.8'

repositories {
    maven { url 'https://jitpack.io' }
    maven { url "https://central.sonatype.com/repository/maven-snapshots/" }
    mavenCentral()
}

dependencies {
    implementation "com.badlogicgames.jnigen:jnigen-loader:$jniGenVersion"
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
}

tasks.withType(JavaCompile) {
    if (JavaVersion.current().isJava9Compatible()) {
        options.compilerArgs += ["--release", "8"]
    }
}

jnigen {
    sharedLibName = "gdx-miniaudio"

    all {
        headerDirs += ["jni"]
        cIncludes  += ["jni/stb_vorbis.c"]
        libraries  += ["-lm", "-lpthread", "-ldl"]

        addPrebuiltCombinedLib(it, "libopusfile_combined")
    }

    addWindows(x32, x86, GCC_CLANG) {
        libraries -= ["-ldl"]
    }
    addWindows(x64, x86, GCC_CLANG) {
        libraries -= ["-ldl"]
    }

    addLinux(x64, x86)
    addLinux(x32, ARM)
    addLinux(x64, ARM)
    addLinux(x64, RISCV)

    addMac(x64, x86) {
        linkerFlags -= ["-mmacosx-version-min=10.7"]
        linkerFlags += ["-mmacosx-version-min=10.13", "-framework", "CoreFoundation", "-framework", "CoreAudio", "-framework", "Foundation", "-framework", "AVFAudio", "-framework", "AudioToolbox"]
    }
    addMac(x64, ARM) {
        linkerFlags -= ["-mmacosx-version-min=10.7"]
        linkerFlags += ["-mmacosx-version-min=11.0", "-framework", "CoreFoundation", "-framework", "CoreAudio", "-framework", "Foundation", "-framework", "AVFAudio", "-framework", "AudioToolbox"]
    }
    addIOS() {
        linkerFlags += ["-framework", "CoreFoundation", "-framework", "CoreAudio", "-framework", "Foundation", "-framework", "AVFAudio", "-framework", "AudioToolbox"]
        cppFlags += ["-x", "objective-c++"]
    }

    robovm {
        minIOSVersion = "15.0"
    }

    addAndroid() {
        libraries -= ["-lpthread"]
        libraries += ["-llog", "-landroid"]
        androidApplicationMk += ["APP_STRIP_MODE := none", "APP_PLATFORM := android-24"]
    }
}

def fetchOpusfile = registerJniZip(
        "fetchLibOpusfile",
        "libopusfile_combined",
        "https://github.com/rednblackgames/opusfile/releases/download/cc04a82/libopusfile-all-platforms.zip",
        "0a6219665af557e4427474606053301ddb9de38804e6bd7a7761b5b46ba2e00f"
)

tasks.register("fetchDependencies") {
    dependsOn(fetchOpusfile)
}

apply from : 'publish.gradle'