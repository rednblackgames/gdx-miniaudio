buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
        gradlePluginPortal()
        mavenLocal()
        google()
        maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
    }
    dependencies {
        classpath "com.badlogicgames.jnigen:jnigen-gradle:$jniGenVersion"
    }
}

plugins {
    id 'java'
}

apply plugin: "com.badlogicgames.jnigen.jnigen-gradle"

group 'games.rednblack.miniaudio'
version '0.8'

repositories {
    maven { url 'https://jitpack.io' }
    maven { url "https://central.sonatype.com/repository/maven-snapshots/" }
    mavenCentral()
}

dependencies {
    implementation "com.badlogicgames.jnigen:jnigen-loader:$jniGenVersion"
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
}

tasks.withType(JavaCompile) {
    if (JavaVersion.current().isJava9Compatible()) {
        options.compilerArgs += ["--release", "8"]
    }
}

import com.badlogic.gdx.jnigen.commons.Os;
import com.badlogic.gdx.jnigen.commons.Architecture;

jnigen {
    sharedLibName = "gdx-miniaudio"

    all {
        headerDirs += ["jni"]
        cIncludes  += ["jni/stb_vorbis.c"]
        libraries  += ["-lm", "-lpthread", "-ldl"]

        def name = os.name().toLowerCase()
        if (os == Os.IOS) {
            name = "ios_" + targetType.platformName
        }

        def arch = architecture.name().toLowerCase() + (architecture == Architecture.x86 && bitness != Architecture.Bitness._32 ? "_" : "") + bitness.toSuffix()
        arch = os == Os.Android ? targetAndroidABI.name().toLowerCase() : arch
        def combined = "${name}_${arch}"

        headerDirs += ["jniLibs/libopusfile/$combined/include", "jniLibs/libopusfile/$combined/include/opus"]
        libraries += [file("jniLibs/libopusfile/$combined/lib/libopusfile_combined.a").absolutePath]
    }

    addWindows(x32, x86, GCC_CLANG) {
        libraries = ["-lm", "-lpthread"]
    }
    addWindows(x64, x86, GCC_CLANG) {
        libraries = ["-lm", "-lpthread"]
    }

    addLinux(x64, x86)
    addLinux(x32, ARM)
    addLinux(x64, ARM)
    addLinux(x64, RISCV)

    addMac(x64, x86) {
        linkerFlags += ["-framework", "CoreFoundation", "-framework", "CoreAudio", "-framework", "Foundation", "-framework", "AVFAudio", "-framework", "AudioToolbox"]
    }
    addMac(x64, ARM) {
        linkerFlags += ["-framework", "CoreFoundation", "-framework", "CoreAudio", "-framework", "Foundation", "-framework", "AVFAudio", "-framework", "AudioToolbox"]
    }
    addIOS() {
        linkerFlags += ["-framework", "CoreFoundation", "-framework", "CoreAudio", "-framework", "Foundation", "-framework", "AVFAudio", "-framework", "AudioToolbox"]
        cppFlags += ["-x", "objective-c++"]
    }
    addAndroid() {
        libraries = ["-llog", "-landroid", "-lm", "-ldl"]
        androidApplicationMk += ["APP_STRIP_MODE := none", "APP_PLATFORM := android-24"]
        androidAndroidMkSharedLibModule += "LOCAL_WHOLE_STATIC_LIBRARIES := opusfile_combined"
        def arch = targetAndroidABI.name().toLowerCase()
        def name = os.name().toLowerCase()
        def combined = "${name}_${arch}"

        def libFile = file("jniLibs/libopusfile/$combined/lib/libopusfile_combined.a").absoluteFile
        def includeDir = file("jniLibs/libopusfile/$combined/include").absoluteFile

        println(libFile)

        androidAndroidMk += [
                "include \$(CLEAR_VARS)",
                "LOCAL_MODULE := opusfile_combined",
                "LOCAL_SRC_FILES := \$(realpath ${libFile})",
                "LOCAL_EXPORT_C_INCLUDES := \$(realpath ${includeDir})",
                "include \$(PREBUILT_STATIC_LIBRARY)"
        ]
    }
}

apply from : 'publish.gradle'